mobj = { SOI ~ obj* ~ EOI }
sobj = {SOI ~ obj ~ EOI}

obj_simple = {
	int
    | "\"" ~ strlit ~ "\""
    | symbol
    | kw
    | atom
}
obj = {
	obj_simple
    | round
    | square
    | curly
    | withmeta
    | quote
    | quasiquote
    | spliceunquote
    | unquote
    | deref
} 
round = { "(" ~ obj* ~ ")" }
square = { "[" ~ obj* ~ "]" }
curly_item = {obj_simple ~ obj}
curly = { "{" ~ curly_item* ~ "}" }
int = @{ "-"? ~ ASCII_DIGIT+ }
strlit = @{ strlit_char* }
strlit_char = _{
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("x" ~ ASCII_HEX_DIGIT{2})
    | "\\" ~ ANY
}
symbol = @{ !kw ~ ident_char+ }
ident_char = _{
	ASCII_ALPHANUMERIC 
    | "_" 
    | "-" 
    | "+" 
    | "/" 
    | "*" 
    | "." 
    | "!" 
    | "?" 
    | "=" 
    | ">" 
    | "<"
    | "&"
}
kw = @{ ("true" | "false" | "nil") ~ !ident_char }
atom = @{ ":" ~ symbol }
withmeta = { "^" ~ obj ~ obj }
quote = { "'" ~ obj }
quasiquote = { "`" ~ obj }
unquote = { "~" ~ obj }
spliceunquote = { "~@" ~ obj }
deref = { "@" ~ obj }

WHITESPACE = _{ " " | "," | "\n" }
COMMENT = _{ ";" ~ (!"\n" ~ ANY)* ~ ("\n" | EOI) }
